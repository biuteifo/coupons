import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.2.0.RELEASE' apply false
}

subprojects {

    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'io.spring.dependency-management'

    group = 'com.alexbob'
    version = '0.0.1-dev'
    sourceCompatibility = '11'

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    idea {
        module {
            outputDir = file("$buildDir/out/")
            testOutputDir = file("$buildDir/test")
        }
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        jcenter()
        mavenCentral()
    }

    ext {
        hutoolVersion = '5.0.0'
        weixinVersion = "3.4.7.B"
        hanlpVersion = "portable-1.7.4"

        springCloudVersion = 'Greenwich.SR2'
        springCloudAlibabaVersion = '2.1.0.RELEASE'
    }

    dependencies {

        implementation "cn.hutool:hutool-all:${hutoolVersion}"
        implementation "org.springframework.boot:spring-boot-actuator"

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'mysql:mysql-connector-java'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.springframework.security:spring-security-test'


        implementation 'org.glassfish.jaxb:jaxb-runtime'
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }

}
