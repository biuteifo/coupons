plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE' apply false
}

subprojects {
    
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: 'io.spring.dependency-management'

    group = 'com.qinke'
    version = '0.0.1-dev'
    sourceCompatibility = '11'

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    idea {
        module {
            outputDir = file("$buildDir/out/")
            testOutputDir = file("$buildDir/test")
        }
    }

    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        jcenter()
        mavenCentral()
    }

    ext {
        hutoolVersion = "4.5.12"
    }

    dependencies {

        implementation "cn.hutool:hutool-all:${hutoolVersion}"

        compileOnly 'org.projectlombok:lombok'

        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
        annotationProcessor 'org.projectlombok:lombok'

        runtimeOnly 'org.springframework.boot:spring-boot-devtools'
        runtimeOnly 'mysql:mysql-connector-java'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'io.projectreactor:reactor-test'
        testImplementation 'org.springframework.security:spring-security-test'
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
        }
    }

}
